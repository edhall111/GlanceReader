apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 6
        versionName "1.0"

        testApplicationId "pro.dbro.glance.steps"
        testInstrumentationRunner "pro.dbro.glance.utils.CucumberInstrumentation"
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
    signingConfigs {
        release {
            // We'll read release signingConfig's data from
            // gradle.properties in the project's root directory
        }
    }

    sourceSets {
        androidTest {
            assets.srcDirs = ['src/androidTest/assets']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
            debuggable false
            zipAlignEnabled true
        }
    }

    packagingOptions {
        exclude 'log4j.properties'
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

// Read Signing key from gradle.properties located in project root
if (project.hasProperty('storeFile')) {
    android.signingConfigs.release.storeFile = file(storeFile)
}
if (project.hasProperty('storePassword')) {
    android.signingConfigs.release.storePassword = storePassword
}
if (project.hasProperty('keyAlias')) {
    android.signingConfigs.release.keyAlias = keyAlias
}
if (project.hasProperty('keyPassword')) {
    android.signingConfigs.release.keyPassword = keyPassword
}

dependencies {
    compile 'net.hockeyapp.android:HockeySDK:3.5.0-b.4'
    compile 'com.github.amlcurran.showcaseview:library:5.0.0'
    compile 'com.facebook.rebound:rebound:0.3.6'
    compile 'org.slf4j:slf4j-android:1.7.7'
    compile group: 'com.google.guava', name: 'guava', version: '20.0'

    compile project(":lib")
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // You must install or update the Support Repository through the SDK manager to use this dependency.
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.squareup:otto:1.3.4'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'
    compile 'com.koushikdutta.ion:ion:1.3.8'

    compile fileTree('libs')

    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support:support-annotations:25.3.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile ('info.cukes:cucumber-android:1.2.2') {
        exclude group: 'com.google.guava'
    }
    androidTestCompile 'info.cukes:cucumber-picocontainer:1.2.0'
}
